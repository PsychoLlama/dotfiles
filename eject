#!/usr/bin/env zsh
set -e

parachute=parachute.tar.gz

function warn {
  echo -e "WARN: $*" >&2
}

source "$(dotfiles dir)/get-manifest"

# Timestamp as <date>--<time>
# Example: 2018-05-18--16-05
date="$(date +'%Y-%m-%d--%H-%M')"
target="$(dotfiles dir)/$date"
mkdir -p "$target"

for file_dest in "${(k)MANIFEST[@]}"; do
  file_source="${MANIFEST[$file_dest]}"

  # Skip files that don't exist.
  if [[ ! -e "$file_source" ]]; then
    warn "No file or directory. Skipping '$file_source'"
    continue
  fi

  # `cp -R` only copies children if directory names end in "/".
  if [[ -d "$file_source" && "${file_source[-1]}" == "/" ]]; then
    file_source="${file_source:0:-1}"
  fi

  # Preserving file attributes & follow symlinks.
  cp -LRp "$file_source" "$target/$file_dest"
done

pushd "$(dotfiles dir)" &> /dev/null
tar -czf "$parachute" "$date" &> /dev/null
rm -rf "$target"
popd &> /dev/null

if [[ "$PWD" != "$(dotfiles dir)" ]]; then
  mv "$(dotfiles dir)/$parachute" ./
fi

echo "$parachute"
